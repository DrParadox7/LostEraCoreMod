// The comments with [HTTPS FIX] tag refers to the parts that fixes the issue.

buildscript {
    repositories {

        // [HTTPS FIX] Replace mavenCentral() with the code block below
        maven {
            url "https://repo1.maven.org/maven2"
        }

        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "forge2"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }

        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

// [HTTPS FIX] Add the code block below
allprojects {
    repositories {
        all { ArtifactRepository repo ->
            if (repo instanceof MavenArtifactRepository) {
                if (repo.url.toString().startsWith("http://repo1.maven.org/maven2")) {
                    repo.url = repo.url.toString().replace("http://", "https://")
                }
            }
        }
    }
}

apply plugin: "forge"

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "1.2"
group = "sexy.poke.Pokepatch" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "PokePatch"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
}

dependencies {
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "FMLCorePlugin": "sexy.poke.PatchCore"
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}
